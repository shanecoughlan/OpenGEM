;**************************************************************************
;*			    DEVICE SPECIFIC ROUTINES			  *
;*									  *   
;*		These routines and equates are device dependant.	  *
;*									  *
;*									  *
;*									  *
;**************************************************************************
;Hist
;Name	Date	Modification
;
include	externs.a86
include af1regs.a86
dseg
        extrn   ptsin_ptr:dword,intin_ptr:dword
cseg
;
	public	INIT_G,DINIT_G
	public	CONCAT
	public	TRAN_FM
	public	EX_TIMV
	public	S_COLMAP
	public	I_COLMAP
	public	CONTRL,INTIN,INTOUT
	public	PTSIN,PTSOUT
	public	PLYGN_BEZ,PLYGN_RROT,MAP_COL,line_offsets
	public	true_ysize
;
	extrn	entry:near		;the place to jump to if this module
;integer array indexing equates
	W_1	equ	word ptr 0
	W_2	equ	word ptr 2
	W_3	equ	word ptr 4
	W_4	equ	word ptr 6
	W_5	equ	word ptr 8
	W_6	equ	word ptr 10
	W_7	equ	word ptr 12
	W_8	equ	word ptr 14
	W_9	equ	word ptr 16
	W_10	equ	word ptr 18
;

;
; Apricot BIOS equates
;
ACTIVE_SCREEN   equ     412h    ;0 for LCD, 1 for 4-colour, 2 for 16-colour
BIOS_MOUSE_FLAG	equ	608h	;Bit 0 set if mouse present
BIOS_CONFIG_TBL	equ	700h	;Pointer to BIOS configuration table
REG_COPY_TABLE  equ     722h    ;Address of write-only-register value copies

ESC		equ	1Bh	

ibmvdi		equ	1		;leave out the old code if for retail
ibmmouse	equ	0
min_x_escape	equ	60
max_x_escape	equ	62
;
;
;****************************************************************
;		IBM STANDARD ROM BIOS CALLS			*
;****************************************************************
direct		equ	false		; direct = true --> don't use ROS calls
					;   (for example, for two-screen stuff)
					; set to false for PCjr

SCREEN	equ	10H			;interrupts to use for rom routine I/O
KEYBD	equ	16H
;
EJECT
;
	jmp	entry		;if we come in from here, go to real entry
;****************************************	
;escape func 2
; Enter Graphics Mode 
;****************************************	
INIT_G:
	push	bp
	push	es
	push	si
	pushf	
	cli
	mov	ax,cs
	mov	usermot+2,ax		;init the user call vectors segs
	mov	userbut+2,ax
	mov	usercur+2,ax
	mov	ax,offset umotvec
	mov	usermot,ax
	mov	ax,offset ubutvec
	mov	userbut,ax
	mov	ax,offset mov_cur
	mov	usercur,ax
	popf
	push	ds
	pop	es
	call	fp_mouse_fixup
	pop	si
	pop	es
	pop	bp
escfn2: 
	cmp	dispmode,1
	jz	escfn2_ok
	ret
escfn2_ok:
	push	bp
	push	es
	push	si
	mov	dispmode,0		; 
	call	vid_detect
	mov	di, offset esc_gfxmode
	call	txt_putstr
	call	update_config
	call	init_pal
	call	init_rowptrs
	call	clearmem
	call	check_mouse
	mov	bx, 32h
	mov	cx, 10h
	mov	dx, 1
	int	0FCh
if mouse
	mov	bl,1
	call	mouse_function		;initialize the mouse
endif
	pop	si
	pop	es
	pop	bp
	ret
;
update_config:
	push	es
	push	ds
	xor	ax, ax
	mov	ds, ax
	mov	bx, BIOS_CONFIG_TBL	;Pointer to Apricot BIOS config table
	les	di, [bx]	;ES:DI->config table
	add	di, 28h
	mov	es:word ptr[di], 20h
	pop	ds
	pop	es
	ret
;
check_mouse:
	push	ds
	xor	ax, ax
	mov	ds, ax
	mov	bx, BIOS_MOUSE_FLAG	
	mov	ax, [bx]
	pop	ds
	test	ax, 1
	jz	check_mouse1
	inc	bx
check_mouse1:
	mov	mouseflag, bl
	ret
;
; Set up the roller-RAM.
;
init_rowptrs:
	mov	ax, 1E0h
	mov	es, ax
	mov	di, 0
	mov	cx, 200
	mov	ax, 200h
	shl	ax, 1
	shl	ax, 1
	shl	ax, 1
	shl	ax, 1
	shr	ax, 1
	cld
init_rowptr1:
	stosw
	add	ax, 50h
	loop	init_rowptr1
	ret
;
init_pal:
	mov	si, offset palette_1
	push	ds
	xor	ax, ax
	mov	ds, ax
	mov	bx, BIOS_CONFIG_TBL
	les	bx, [bx]
	add	bx, 23h		;Colour / mono flag?
	mov	al, es:[bx]
	test	al, 0FFh
	jz	init_pal_1
	mov	si, offset palette_2
init_pal_1:
	pop	ds
	push	cs
	pop	es
	mov	di, offset palette
	mov	cx, 104
	cld
	rep	movsw
	mov	si, offset palette
	mov	bx, 0FFh
	call	write_lut	
	ret
;
calc_lutreg:
	mov	dx, ax
	mov	al, ah	
	test	al, 2
	jz	calc_lutreg_1
	or	ah, 8
	jmps	calc_lutreg_2
;
calc_lutreg_1:
	and	ah, 0F7h
calc_lutreg_2:
	test	al, 8
	jz	calc_lutreg_3
	or	ah, 2
	jmps	calc_lutreg_4
;
calc_lutreg_3:
	and	ah, 0FDh
calc_lutreg_4:
	not	ah
	and	ah, 0Fh
	mov	al, dl
	ret
;
write_lut:
	or	bx, bx
	jnz	write_lut1
	push	es
	call	calc_lutreg
	mov	bx, 0E000h
	mov	es, bx
	shl	al, 1
	shl	al, 1
	mov	dl, ah
	xor	ah, ah
	mov	si, ax
	add	si, offset sp_col
	mov	cx, 4	;Load a single palette register, 4 times
	xor	bx, bx	;(because this is a 4-colour mode)
write_lut0:
	lodsb
	mov	bl, al
	mov	es:[bx], dl
	loop	write_lut0
	pop	es
	ret
;
write_lut1:
	mov	di, offset sp_col
	mov	cx, 4
	mov	ah, 0
write_lut2:
	mov	al, cs:[si]
	inc	si
	xor	bx, bx
	xchg	ah, al
	push	ax
	push	si
	push	cx
	call	write_lut
	pop	cx
	pop	si
	pop	ax
	xchg	ah, al
	inc	ah
	loop	write_lut2
	ret
;

;****************************************	
;escape func 3
; Exit Graphics Mode
;****************************************	
DINIT_G:
escfn3: 
	cmp	dispmode,0
	jz	escfn3_ok
	ret
escfn3_ok:				; 
if mouse
	mov	bl,2
	call	mouse_function
endif
	mov	dispmode,1
	call	CLEARMEM
	push	ds
	xor	ax, ax
	mov	ds, ax
	mov	bx, BIOS_CONFIG_TBL
	les	di, [bx]		;BIOS config table
	add	di, 28h
	mov	es:word ptr [di], 0FFFFh
	pop	ds
	mov	di, offset esc_z
	call	txt_putstr
	ret
EJECT
;****************************************************************
;I_COLMAP							*	
;	ENTRY	                                        	*
;		INTIN[0] = color index				*
;		INTIN[1] = req or actual flag			*
;								*
;	EXIT	                             			*
;		INTOUT[0] = color index				*
;		INTOUT[1] = RED					*
;		INTOUT[2] = GRN					*
;		INTOUT[3] = BLU					*
;								*
;	Reg. Used. AX, BX, CX, DX, DI				*
;								*
;****************************************************************
I_COLMAP:
	mov	bx, INTIN		;check if the index is in range of dev
	cmp	bx, 0
	jge	i_colmap_chkix
	jmps	i_colmap_badix
i_colmap_chkix:
	cmp	bx, num_colors-1	;test if index is too high
	jle	i_colmap_ixok
	jmps	i_colmap_badix
i_colmap_ixok:
	shl	bx, 1
	mov	bx, MAP_COL[bx]
	mov	di, bx			; di = ptr to act/req col tbls
	shl	di, 1
	shl	di, 1
	add	di, bx
	add	di, bx			; mpy ix by 6 to point into table
	mov	si, offset INTOUT+2
	mov	cx, 3
	mov	INTOUT, bx		; return the color index
	test	INTIN+2, 0ffffh		; if != 0 then return actual values
	jz	i_colmap_load
	jmp	i_colmap_act
i_colmap_load:
	mov	bx, offset req_col_tbl
	mov	ax, cs:[bx+di]
	mov	[si], ax
	add	di, 2
	add	si, 2
	loop	i_colmap_load
	ret
i_colmap_badix:
	mov	INTOUT, -1
	ret	
;
i_colmap_act:
	mov	dl, cs:byte ptr palette[bx]
	mov	dh, 1
	xor	ax, ax
	xor	bx, bx
	test	dl, dh
	jz	i_colmap_act1
	mov	bx, 250
i_colmap_act1:
	mov	dh, 8
i_colmap_act2:
	mov	ax, bx
	test	dl, dh
	jz	i_colmap_act3
	add	ax, 500
i_colmap_act3:
	mov	[si], ax
	add	si, 2
	shr	dh, 1
	loop	i_colmap_act2
	ret
	
EJECT
;****************************************************************
;S_COLMAP							*	
;	ENTRY	                                        	*
;		INTIN[0] = color index				*
;		INTIN[1] = RED 0 - 1000				*
;		INTIN[1] = GRN 0 - 1000				*
;		INTIN[1] = BLU 0 - 1000				*
;								*
;	EXIT	                             			*
;		                           			*
;								*
;	Reg. Used. AX, BX, CX, DX, DI				*
;								*
;****************************************************************
S_COLMAP:
	mov	bx, INTIN		;check if the index is in range of dev
	cmp	bx, 0
	jge	s_colmap_chkix
	ret
s_colmap_chkix:
	cmp	bx, num_colors-1	;test if index is too high
	jle	s_colmap_ixok
	ret
s_colmap_ixok:
	shl	bx, 1
	mov	bx, MAP_COL[bx]
	mov	di, bx			; di = ptr to act/req col tbls
	shl	di, 1
	shl	di, 1
	add	di, bx
	add	di, bx			; mpy ix by 6 to point into table
	push	bx
	xor	dx, dx
	xor	bx, bx
	mov	si, 2
	mov	cx, 3
	mov	s_colmap_i, 0
s_colmap_loop:
	mov	ax, INTIN[si]
	mov	cs:req_col_tbl[di], ax
	add	si, 2
	add	bx, ax
	cmp	ax, 750
	jc	s_colmap_no_i
	mov	s_colmap_i, 1
s_colmap_no_i:
	cmp	ax, 500
	jc	s_colmap_no_c
	or	dx, 1
s_colmap_no_c:
	shl	dx, 1
	add	di, 2
	loop	s_colmap_loop
	cmp	s_colmap_i, 1
	jnz	s_colmap_set
	or	dx, 1
s_colmap_set:
	or	dx, dx
	jnz	s_colmap_not0
	mov	ax, bx	;Check for dark grey -> bright black
	mov	bx, 3
	div	bx
	xor	dx, dx
	cmp	ax, 250
	jc	s_colmap_not0
	or	dx, 1
s_colmap_not0:
	pop	bx
	mov	cs:byte ptr palette[bx], dl
	mov	al, bl
	xor	bx, bx
	mov	ah, dl
	call	write_lut
	ret	


;****************************************************************
;TRAN_FM							*	
;	ENTRY	                                        	*
;		                                     		*
;								*
;	EXIT	                             			*
;		                           			*
;								*
;	Reg. Used. AX, BX, CX, DX, DI				*
;								*
;****************************************************************
TRAN_FM:
	push	bp	
	push	es
	push	ds
	les	di,contrl_ptr
	mov	ax, es: word ptr 14[di]
	mov	bx, es: word ptr 16[di]		; get smfdb pointer
	mov	dx, es: word ptr 18[di]
	mov	bp, es: word ptr 20[di]		; get dmfdb pointer
	mov	es, bx
	mov	di, ax				; set es:di to it
	mov	ax, es: word ptr 12[di]		; get num of planes in source
; xform n to n planes
	push	ax				; save the plane count
	call	calc_src_form
	call	calc_des_form
	call	calc_seg_off
	pop	bx				; get the plane count back
col_to_col_lp:
	push	si
	push	di
	push	bx
	push	cx
col_to_col_loop:
	lodsw
if not byte_swap
	xchg	ah,al
endif
if rev_vid
	not	ax
endif
	stosw
	loop	col_to_col_loop
	pop	cx
	pop	bx
	pop	di
	pop	si
	call	tran_next_seg
	dec	bx
	jnz	col_to_col_lp
	pop	ds
	pop	es
	pop	bp
	ret
tran_next_seg:
	push	cx
	mov	ax, cx
	mov	cl, 3
	shr	ax, cl			;get the segment increment
	mov	bp, es
	add	bp, ax
	mov	es, bp
	mov	bp, ds
	add	bp, ax
	mov	ds, bp
	pop	cx
	mov	ax, cx			;get the byte increment
	and	ax, 0007h
	shl	ax, 1
	add	si, ax
	add	di, ax
	ret
;
calc_seg_off:
	mov	bp, cx				; find the next seg
	shr	bp,1
	shr	bp,1
	shr	bp,1
	mov	dx, cx
	shl	dx, 1
	and	dx, 000fh			; find the incremental offset
;	mov	bx,num_planes
	ret
;
calc_des_form:
	mov	ds, bp
	mov	si, dx				; load dmfdb pointer
	not	bx
	and	bx,1				; invert format
	mov	10[si],bx			; toggle dest form
	mov	ax, es: word ptr 2[di]		; get source ptr seg
	mov	di, es: word ptr [di]		; offset
	mov	es, ax
	mov	ax, 2[si]
	mov	si, [si]			; get dest ptr seg,offset
	mov	ds, ax
	xchg	si,di				; swap pointers
	mov	ax, es
	mov	dx, ds
	mov	es, dx
	mov	ds, ax
	ret
;
calc_src_form:
	mov	bx, es: word ptr 10[di]		; get the format flag
	mov	ax, es: word ptr 8[di]		; get the width in words
	mov	cx, es: word ptr 6[di]		; get the height in pixels
	push	dx
	mul	cx
	pop	dx
	mov	cx, ax				; cx = word count
	and	bx,bx				; if bx = 0 then dev spec form
	ret

EJECT
;****************************************************************
;EX_TIMV							*	
;	ENTRY	                                        	*
;		CONTRL 7,8 = NEW VEC                            *
;	EXIT	                             			*
;		                           			*
;		CONTRL 9,10 = OLD VEC				*
;               INTOUT [0] = TIMER TICK COUNT IN MILLISECS	*
;								*
;****************************************************************
EX_TIMV:
	pushf
	cli
	push	es
	push	ds
	les	di,contrl_ptr
	xor	ax,ax
	mov	ds,ax
	mov	si,4 * 0FFh		;setup the offset to timer int vector
	mov	ax,[si]
	mov	es:word ptr 18[di],ax	;save old timer vector
	mov	ax,2[si]
	mov	es:word ptr 20[di],ax
	mov	ax, es:word ptr 14[di]
	mov	[si],ax
	mov	ax, es:word ptr 16[di]
	mov	2[si],ax
	pop	ds
	pop	es
	popf
	mov	ax, 1			; flag that data is to be returned 
	mov	CONTRL+8, ax
	mov	ax, 11h
	mov	INTOUT, ax		; return the millisec/tick count 
	ret
;
EJECT
;
txt_putstr:	
	mov	ax, ds
	mov	es, ax
	mov	cx, [di]		; Length of string
	add	di, 2
txt_puts1:
	jcxz	txt_puts2
	mov	ax, es:[di]
	add	di, 2
	call	txt_putchar
	loop	txt_puts1
txt_puts2:
	ret
;
txt_putchar:
	mov	dl, al
	mov	ah, 2
	int	21h
	ret
;

EJECT
;****************************************************************
;CONCAT 							*	
;	ENTRY	Reg. BX - X-coordinate (dev. coord.)		*
;		Reg. AX - Y-coordinate (dev. coord.)		*
;								*
;	EXIT	Reg. DI - Physical address			*
;		Reg. BL - Byte index				*	
;								*
;	Reg. Used. AX, BX, CX, DX, DI				*
;								*
;****************************************************************
concat:
	and	bx, 03ffh		;mask off 10 lsb for x-coord.
	mov	cx, bx
	mov	di, ax
	shl	di, 1
	mov	di, line_offsets[di]
	sar	cx, 1
	sar	cx, 1
	and	cx, 0FFFEh
	add	di, cx
	and	bl, 7
	xor	bh, bh
	ret

	cseg
EJECT
include detect.a86
include	aprfdvsp.a86
if mouse
include fpmouse.a86
endif
cseg
include aprfmre1.a86
include aprfmre2.a86
dseg
include af1dev.a86

	dseg

		dw	0,0,0,0
line_deltax	dw	0
line_deltay	dw	0
line_drawaddr	dw	0
line_nextrow	dw	0
line_step	dw	0
		dw	0
line_ormask	dw	0
line_dxparam	dw	0
line_deltac	dw	0
line_nadd_dx	dw	0
line_add_dx	dw	0
line_prefix	dw	0
line_draw	dw	0
line_drawcode	dw	0,0
line_arg_offset	dw	0
		dw	0
line_flg1	db	0
line_initjump	db	0
line_flg3	db	0
		db	0
line_prefixes	dw	offset line_prefix_rep
line_draws	dw	offset line_draw_rep
		dw	offset line_prefix_rep	
		dw	offset line_draw_rep	
		dw	0
		dw	offset line_draw_tran
		dw	0
		dw	offset line_draw_xor
		dw	0
		dw	offset line_draw_tran

line_argp	dw	0FFFAh, 0
		dw	0FFFAh, 0
		dw	0FFFAh, 0
		dw	0FFFAh, 0
		dw	0, 0



right_masks	dw	0FFFFh, 07FFFh, 03FFFh, 01FFFh
		dw	00FFFh, 007FFh, 003FFh, 001FFh
		dw	000FFh, 0007Fh, 0003Fh, 0001Fh
		dw	0000Fh, 00007h, 00003h, 00001h
left_masks	dw	0FFFFh, 0FFFEh, 0FFFCh, 0FFF8h
		dw	0FFF0h, 0FFE0h, 0FFC0h, 0FF80h
		dw	0FF00h, 0FE00h, 0FC00h, 0F800h
		dw	0F000h, 0E000h, 0C000h, 08000h

if msb_first
ortbl		db	128		; 'or' mask table in stpixl
		db	64
		db	32
		db	16
		db	8
		db	4
		db	2
		db	1
else
ortbl		db	1
		db	2
		db	4
		db	8
		db	16
		db	32
		db	64
		db	128
endif
;
line_offsets	dw	0, 160, 320, 480, 640, 800, 960, 1120, 1280, 1440
		dw	1600, 1760, 1920, 2080, 2240, 2400, 2560, 2720, 2880
		dw	3040, 3200, 3360, 3520, 3680, 3840, 4000, 4160, 4320
		dw	4480, 4640, 4800, 4960, 5120, 5280, 5440, 5600, 5760
		dw	5920, 6080, 6240, 6400, 6560, 6720, 6880, 7040, 7200
		dw	7360, 7520, 7680, 7840, 8000, 8160, 8320, 8480, 8640
		dw	8800, 8960, 9120, 9280, 9440, 9600, 9760, 9920
		dw	10080, 10240, 10400, 10560, 10720, 10880, 11040, 11200
		dw	11360, 11520, 11680, 11840, 12000, 12160, 12320, 12480
		dw	12640, 12800, 12960, 13120, 13280, 13440, 13600, 13760
		dw	13920, 14080, 14240, 14400, 14560, 14720, 14880, 15040
		dw	15200, 15360, 15520, 15680, 15840, 16000, 16160, 16320
		dw	16480, 16640, 16800, 16960, 17120, 17280, 17440, 17600
		dw	17760, 17920, 18080, 18240, 18400, 18560, 18720, 18880
		dw	19040, 19200, 19360, 19520, 19680, 19840, 20000, 20160
		dw	20320, 20480, 20640, 20800, 20960, 21120, 21280, 21440
		dw	21600, 21760, 21920, 22080, 22240, 22400, 22560, 22720
		dw	22880, 23040, 23200, 23360, 23520, 23680, 23840, 24000
		dw	24160, 24320, 24480, 24640, 24800, 24960, 25120, 25280
		dw	25440, 25600, 25760, 25920, 26080, 26240, 26400, 26560
		dw	26720, 26880, 27040, 27200, 27360, 27520, 27680, 27840
		dw	28000, 28160, 28320, 28480, 28640, 28800, 28960, 29120
		dw	29280, 29440, 29600, 29760, 29920, 30080, 30240, 30400
		dw	30560, 30720, 30880, 31040, 31200, 31360, 31520, 31680
		dw	31840, 32000, 32160, 32320, 32480, 32640, 32800, 32960
		dw	33120, 33280, 33440, 33600, 33760, 33920, 34080, 34240
		dw	34400, 34560, 34720, 34880, 35040, 35200, 35360, 35520
		dw	35680, 35840, 36000, 36160, 36320, 36480, 36640, 36800
		dw	36960, 37120, 37280, 37440, 37600, 37760, 37920, 38080
		dw	38240, 38400, 38560, 38720, 38880, 39040, 39200, 39360
		dw	39520, 39680, 39840, 40000, 40160, 40320, 40480, 40640
		dw	40800
box_optbl	dw	offset	box_replace_mode
		dw	offset	box_tran_mode
		dw	offset	box_xor_mode
		dw	offset	box_invtran_mode
s_colmap_i	db	0
box_pattern	dw	0
		rb	34
sp_col		db	0, 2, 8, 10, 4, 6, 12, 14, 16, 18, 24, 26, 20, 22, 28
		db	30, 0, 0, 2, 2, 3, 3, 3, 3, 4, 4, 6, 6, 5, 1, 7, 7, 0
;


include scanxlt.a86
;

txt_row		dw	1
txt_col		dw	1
mouseflag	db	0
esc_gfxmode	dw	2, ESC, '}'
esc_z		dw	2, ESC, 'z'
esc_H		dw	2, ESC, 'H'
esc_K		dw	2, ESC, 'K'
esc_p		dw	2, ESC, 'p'
esc_q		dw	2, ESC, 'q'
esc_y		dw	4, ESC, 'Y', ' ', ' '

dispmode	dw	1
true_yresmx	dw	199
true_ysize	dw	620
;
		cseg
palette		rw	8
act_col_tbl	rw	48
req_col_tbl	rw	48
		dseg

palette_1	db	15,9,5,0,9,45,27,0,63,4,2,6,1,5,3,0
		dw	 999,  999,  999
		dw	 750,    0,    0
		dw	   0,  750,    0
		dw	   0,    0,    0
		dw	   0,    0, 1000
		dw	1000,    0, 1000
		dw	   0, 1000, 1000
		dw	1000, 1000, 1000
		dw	   0,    0,    0
		dw	 333,    0,    0
		dw	   0,  333,    0
		dw	 333,  333,    0
		dw	   0,    0,  333
		dw	 333,    0,  333
		dw	   0,  333,  333
		dw	 333,  333,  333
		dw	 999,  999,  999
		dw	 750,    0,    0
		dw	   0,  750,    0
		dw	   0,    0,    0
		dw	   0,    0, 1000
		dw	1000,    0, 1000
		dw	   0, 1000, 1000
		dw	1000, 1000, 1000
		dw	   0,    0,    0
		dw	 333,    0,    0
		dw	   0,  333,    0
		dw	 333,  333,    0
		dw	   0,    0,  333
		dw	 333,    0,  333
		dw	   0,  333,  333
		dw	 333,  333,  333
palette_2	db	3,9,0,13,5,11,7,15,1,8,4,12,2,10,6,14
		dw	   0,    0,  999
		dw	 999,    0,    0
		dw	   0,    0,    0
		dw	 999,  999,    0
		dw	   0,  999,    0
		dw	 999,    0,  999
		dw	   0,  999,  999
		dw	 999,  999,  999
		dw	 260,  260,  260
		dw	 600,    0,    0
		dw	   0,  600,    0
		dw	 600,  600,    0
		dw	   0,    0,  600
		dw	 600,    0,  600
		dw	   0,  600,  600
		dw	 600,  600,  600
		dw	   0,    0,  999
		dw	 999,    0,    0
		dw	   0,    0,    0
		dw	 999,  999,    0
		dw	   0,  999,    0
		dw	 999,    0,  999
		dw	   0,  999,  999
		dw	 999,  999,  999
		dw	 260,  260,  260
		dw	 600,    0,    0
		dw	   0,  600,    0
		dw	 600,  600,    0
		dw	   0,    0,  600
		dw	 600,    0,  600
		dw	   0,  600,  600
		dw	 600,  600,  600

MAP_COL		dw	0	;F1 appears to assume the palette has 32 entries.
		dw	3		;red
		dw	1		;green
		dw	2		;blue
		dw	3		;cyan
		dw	3		;yellow	
		dw	3		;magenta
		dw	3		;white
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3

		dw	0
		dw	3
		dw	1
		dw	2
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3
		dw	3

CONTRL          rw      CONTRL_SIZE
INTIN           rw      INTIN_SIZE
PTSOUT          rw      PTSOUT_SIZE
	include	date.a86
	db	' Apricot FP 3-plane colour driver '
	include	copyrt.a86
		rs offset PTSIN + 2 * PTSIN_SIZE + 4 - offset $

INTOUT		rw INTOUT_SIZE


